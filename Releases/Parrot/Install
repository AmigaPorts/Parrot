; $VER: Parrot Installer (08.11.2020)
; Installer for Parrot 1.2+
; (c) 2020 Robin Southern
; https://github.com/betajaen/parrot

; Constants
(set @app-name ("Parrot"))
(set @app-version ("1.2"))
(set #parrot_default_path "Work:")

; Welcome
(welcome "Welcome to the " @app-name " " @app-version " installer.")
(complete 0)

; Path
(set #parrot_path
	(askdir
		(prompt "Choose a Drawer for Parrot to be installed to. A Parrot drawer will be created automatically")
		(help "Work:Games/ is a good place to put Parrot")
		(default #parrot_default_path)
	)
)

(set #parrot_path (tackon #parrot_path "Parrot"))
(set #parrot_tools_path (tackon #parrot_path "Tools"))
; (set #parrot_documentation_path (tackon #parrot_path "Documentation"))
(set @default-dest #parrot_path)

(complete 10)

; Games
(set #parrot_game_bits
	(askoptions
		(prompt "What Games would you want to play?")
		(choices "Maniac Mansion")
		(default 1)
		(help "Each game data must be converted into a Parrot friendly format before use. Choosing a game allows that data to be converted during the first play.")
	)
)

(set #parrot_game_maniac (BITAND (shiftright #parrot_game_bits 0) 1))
;(set #parrot_game_2 (BITAND (shiftright #parrot_game_bits 1) 1))
;(set #parrot_game_3 (BITAND (shiftright #parrot_game_bits 2) 1))
;(set #parrot_game_4 (BITAND (shiftright #parrot_game_bits 3) 1))

; Installation - Essentials

(copyfiles 
		(prompt "Copying Parrot and icon?")
		(help "The Parrot executable is required to play Parrot games")
		(infos)
		(source "")
		(dest #parrot_path)
		(choices "Parrot" "Parrot.readme")
)
(complete 50)

; Installation - Documentation

; @FUTURE RELEASE


; Installation - Maniac Mansion
(if (= #parrot_game_maniac 1)
	(
		(copyfiles 
			(prompt "Copying Maniac Mansion Conversion Tool and Icon")
			(help "The Maniac Mansion Conversion tool reinterprets and recompiles the Amiga version of Maniac Mansion to a Parrot compatible format")
			(infos)
			(source "Tools")
			(dest #parrot_tools_path)
			(choices "ConvertManiac")
		)
	)
)
(complete 86)


; Complete
(complete 100)
(exit bye)